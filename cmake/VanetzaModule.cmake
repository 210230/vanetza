macro(vanetza_module NAME)
    set(_sources ${ARGN})
    add_library(${NAME} SHARED ${_sources})
    add_library(${NAME}_static STATIC ${_sources})
    set_property(TARGET ${NAME} PROPERTY OUTPUT_NAME vanetza_${NAME})
    set_property(TARGET ${NAME}_static PROPERTY OUTPUT_NAME vanetza_${NAME})
    target_include_directories(${NAME} PUBLIC ${VANETZA_MODULE_INCLUDE_DIRECTORIES})
    target_include_directories(${NAME}_static PUBLIC ${VANETZA_MODULE_INCLUDE_DIRECTORIES})
    install(TARGETS ${NAME} ${NAME}_static EXPORT ${PROJECT_NAME} DESTINATION lib)
endmacro()

macro(vanetza_module_dependencies NAME TYPE)
    set(_deps ${ARGN})
    if(NOT ${TYPE} STREQUAL "LINKING")
        list(APPEND _deps ${TYPE})
        set(TYPE "DEPENDS")
    endif()
    foreach(_dep ${_deps})
        if(${TYPE} STREQUAL "LINKING")
            target_link_libraries(${NAME} ${_dep})
            target_link_libraries(${NAME}_static ${_dep})
        else()
            add_dependencies(${NAME} ${_dep})
            add_dependencies(${NAME}_static ${_dep})
        endif()
    endforeach()
endmacro()

macro(vanetza_intermodule_dependencies NAME)
    set(_modules ${ARGN})
    foreach(_module ${_modules})
        target_link_libraries(${NAME} ${_module})
        target_link_libraries(${NAME}_static ${_module}_static)
    endforeach()
endmacro()

macro(vanetza_module_property NAME)
    set_property(TARGET ${NAME} ${NAME}_static APPEND PROPERTY ${PROPERTY} ${ARGN})
endmacro()

macro(vanetza_export_modules)
    string(TOLOWER ${PROJECT_NAME} _project_name_lower)
    export(EXPORT ${PROJECT_NAME} NAMESPACE Vanetza:: FILE ${_project_name_lower}-targets.cmake)
    file(WRITE ${PROJECT_BINARY_DIR}/vanetza-config.cmake
        "include(\"${PROJECT_BINARY_DIR}/${_project_name_lower}-targets.cmake\")")
    export(PACKAGE ${PROJECT_NAME})
endmacro()

